// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
    // zx, zy
    Mux16(a = x, b[0..15] = false, sel = zx, out = outZX);
    Mux16(a = y, b[0..15] = false, sel = zy, out = outZY);

    // nx, ny
    Not16(in = outZX, out=notX);
    Not16(in = outZY, out=notY);
    Mux16(a = outZX, b = notX, sel = nx, out = outNZX);
    Mux16(a = outZY, b = notY, sel = ny, out = outNZY);

    // f
    And16(a = outNZX, b = outNZY, out = andNZXY);
    Add16(a = outNZX, b = outNZY, out = addNZXY);
    Mux16(a = andNZXY, b = addNZXY, sel = f, out = outFXY);

    // no, ng
    Not16(in = outFXY, out = outNFXY);
    Mux16(a = outFXY, b = outNFXY, sel = no, 
        out = out, 
        out[15] = ng, 
        out[0..7] = preZR1,
        out[8..15] = preZR2
    );

    // zr
    Or8Way(in = preZR1, out = outZR1);
    Or8Way(in = preZR2, out = outZR2);
    Or(a = outZR1, b = outZR2, out = preZR);
    Not(in = preZR, out = zr);
}